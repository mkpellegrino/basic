10 REM TRANSLATE A SENTENCE INTO PIG LATIN
20 REM BY MR PELLEGRINO
30 REM 2020 03 09
31 REM
40 REM INITIALIZE VARIABLES
41 REM
50 LET A$="":LET B$="":LET C$="":LET X$="":LET D$="":LET L=0
59 REM
60 REM DISPLAY USER INSTRUCTIONS
61 REM
70 HOME
71 PRINT "LANGUAGE TO PIG LATIN TRANSLATOR"
72 PRINT " BY MR. PELLEGRINO - 2020 03 09"
73 PRINT:PRINT
78 PRINT "   ENTER A SENTENCE AND I WILL":PRINT "   TRANSLATE IT INTO PIG LATIN":PRINT
79 REM
80 REM TELL COMPUTER TO GET A SENTENCE FROM THE USER
81 REM
90 INPUT X$:PRINT
99 REM
100 REM GET FIRST WORD BY LOOKING FOR A SPACE
110 REM TRANSLATE IT AND THEN GET RID OF THAT WORD
111 REM
120 L=0
130 IF MID$(X$,L,1)=" " THEN GOTO 160
140 IF L>LEN(X$) GOTO 160
150 L=L+1:GOTO 130
160 L=L-1 : REM Sub 1 from L BECAUSE THE LOOP IS COUNTING THE SPACE
170 A$=LEFT$(X$,L):X$=RIGHT$(X$,LEN(X$)-L-1)
179 REM
180 REM CHECK TO SEE IF THE FIRST LETTER IS A VOWEL
190 REM IF IT IS THEN MAKE B$ = THE WORD WITH A "WAY" AT THE END
191 REM
200 IF LEFT$(A$,1)="A" THEN B$=A$+"WAY": GOTO 380
210 IF LEFT$(A$,1)="E" THEN B$=A$+"WAY": GOTO 380
220 IF LEFT$(A$,1)="I" THEN B$=A$+"WAY": GOTO 380
230 IF LEFT$(A$,1)="O" THEN B$=A$+"WAY": GOTO 380
240 IF LEFT$(A$,1)="U" THEN B$=A$+"WAY": GOTO 380
249 REM
250 REM NOW CHECK TO SEE IF THE SECOND LETTER IS A VOWEL
260 REM IF IT _IS_ THEN THAT MEAN THAT THE FIRST LETTER IS
270 REM A CONSONANT AND THE 2ND LETTER IS A VOWEL
271 REM
280 IF MID$(A$,2,1)="A" THEN GOTO 370
290 IF MID$(A$,2,1)="E" THEN GOTO 370
300 IF MID$(A$,2,1)="I" THEN GOTO 370
310 IF MID$(A$,2,1)="O" THEN GOTO 370 
320 IF MID$(A$,2,1)="U" THEN GOTO 370
329 REM
330 REM THE FIRST 2 LETTERS MUST BE CONSONANTS
331 REM
340 B$=RIGHT$(A$,LEN(A$)-2):C$=LEFT$(A$,2):B$=B$+C$+"AY"
350 GOTO 380
359 REM
360 REM THE FIRST LETTER IS A CONSONANT
361 REM
370 B$=RIGHT$(A$,LEN(A$)-1):C$=LEFT$(A$,1):B$=B$+C$+"AY"
379 REM
380 REM DISPLAY THE TRASLATED WORD
381 REM
390 PRINT B$;:PRINT " ";
400 IF LEN(X$)=0 THEN 420
410 GOTO 100
419 REM
420 REM ASK THE USER IF THEY WANT TO ENTER ANOTHER PHRASE
421 REM
430 PRINT:PRINT:PRINT "Again? (Y/N)"
440 INPUT X$:REM REUSE THE X$ VARIABLE HERE
450 IF LEFT$(X$,1)="Y" THEN GOTO 60
460 IF LEFT$(X$,1)="y" THEN GOTO 60
469 REM
470 REM END THE PROGRAM
471 REM
480 END
