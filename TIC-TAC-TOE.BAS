10 REM 2 PLAYER TIC-TAC-TOE
20 REM By: Mr. Pellegrino and E-Block Class
30 REM October 7, 2019 - v1.0
40 REM
50 HOME:PRINT "Two Player Tic-Tac-Toe By Mr. Pellegrino"
60 PRINT "October 7, 2019 - v1.0"
70 PRINT "Press Enter"
80 INPUT X$
100 REM INSTRUCTIONS
110 HOME:PRINT "Enter a ColRow for a valid Move"
120 PRINT "Like: A2, B3, or C1":PRINT "NOT 2A, 3B, or 1C!"
130 PRINT "Press Enter"
140 INPUT X$
300 REM CHOOSE X's or O's
310 PRINT "Player 1, X or O?"
320 INPUT P1$
330 IF P1$="X" then P2$="O":GOTO 360
340 IF P1$="O" then P2$="X":GOTO 360
350 PRINT "X or O please":goto 310
360 PRINT "P1: "; P1$:PRINT "P2: "; P2$

400 REM INITIALIZE BOARD AND VARIABLES
410 A1$=" ":A2$=" ":A3$=" "
420 B1$=" ":B2$=" ":B3$=" "
430 C1$=" ":C2$=" ":C3$=" "
440 C=0:P=1

500 REM SHOW THE BOARD
510 GOSUB 3000

600 REM GET A MOVE FROM THE USER
610 IF P=1 THEN P$=P1$
620 IF P=0 THEN P$=P2$
630 INPUT M$
640 IF (M$="A1" AND A1$=" ") THEN A1$=P$:GOTO 740
650 IF (M$="A2" AND A2$=" ") THEN A2$=P$:GOTO 740
660 IF (M$="A3" AND A3$=" ") THEN A3$=P$:GOTO 740
670 IF (M$="B1" AND B1$=" ") THEN B1$=P$:GOTO 740
680 IF (M$="B2" AND B2$=" ") THEN B2$=P$:GOTO 740
690 IF (M$="B3" AND B3$=" ") THEN B3$=P$:GOTO 740
700 IF (M$="C1" AND C1$=" ") THEN C1$=P$:GOTO 740
710 IF (M$="C2" AND C2$=" ") THEN C2$=P$:GOTO 740
720 IF (M$="C3" AND C3$=" ") THEN C3$=P$:GOTO 740
730 PRINT "INVALID MOVE!  TRY AGAIN!":GOTO 500
740 C=C+1: REM ADD 1 TO THE NUMBER OF VALID MOVES
800 REM CHECK FOR A WINNER OR TIE
810 IF (A1$="X" AND A2$="X" AND A3$="X") THEN GOTO 2000
820 IF (B1$="X" AND B2$="X" AND B3$="X") THEN GOTO 2000
830 IF (C1$="X" AND C2$="X" AND C3$="X") THEN GOTO 2000
840 IF (A1$="X" AND B1$="X" AND C1$="X") THEN GOTO 2000
850 IF (A2$="X" AND B2$="X" AND C2$="X") THEN GOTO 2000
860 IF (A3$="X" AND B3$="X" AND C3$="X") THEN GOTO 2000
870 IF (A1$="X" AND B2$="X" AND C3$="X") THEN GOTO 2000
880 IF (A3$="X" AND B2$="X" AND C1$="X") THEN GOTO 2000

910 IF (A1$="O" AND A2$="O" AND A3$="O") THEN GOTO 2000
920 IF (B1$="O" AND B2$="O" AND B3$="O") THEN GOTO 2000
930 IF (C1$="O" AND C2$="O" AND C3$="O") THEN GOTO 2000
940 IF (A1$="O" AND B1$="O" AND C1$="O") THEN GOTO 2000
950 IF (A2$="O" AND B2$="O" AND C2$="O") THEN GOTO 2000
960 IF (A3$="O" AND B3$="O" AND C3$="O") THEN GOTO 2000
970 IF (A1$="O" AND B2$="O" AND C3$="O") THEN GOTO 2000
980 IF (A3$="O" AND B2$="O" AND C1$="O") THEN GOTO 2000
990 IF C=9 THEN GOTO 2100 : REM TIE


1000 REM SWITCH USER AND LOOP
1010 IF P=1 THEN P=0:GOTO 1030
1020 IF P=0 THEN P=1
1030 GOTO 500
2000 REM WINNER
2010 IF P=1 THEN PRINT " Player 1 Wins!":PRINT 
2020 IF P=0 THEN PRINT " Player 2 Wins!":PRINT 
2021 GOSUB 3000
2030 GOTO 2500
2100 REM TIE
2110 PRINT "TIE GAME":PRINT :GOSUB 3000


2500 rem Play Again?
2510 PRINT "Would you like to play another game?":PRINT "(Y/N)"
2520 INPUT X$
2530 IF X$="Y" THEN 300
2540 IF X$="N" THEN END
2550 PRINT "ENTER Y or N ":goto 2510


3000 REM SHOW THE BOARD
3010 PRINT "  A B C":PRINT :PRINT "1 ";A1$;"|";B1$;"|";C1$:PRINT "  -+-+-":PRINT "2 ";A2$;"|";B2$;"|";C2$:PRINT "  -+-+-":PRINT "3 ";A3$;"|";B3$;"|";C3$
3020 RETURN
